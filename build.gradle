plugins {
    id 'java'
    id 'application'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

group = 'org.moddingx'
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

configurations {
    lib
    api.extendsFrom lib
    libImpl.extendsFrom lib
    implementation.extendsFrom libImpl
    libResolvable.extendsFrom libImpl
}

sourceSets {
    lib {
        compileClasspath = configurations.libResolvable
        java { srcDirs = [ file('src/main/java') ] }
        resources { srcDirs = [ file('src/main/resources') ] }
    }
    main { // Main source set needs to be plugin source set
        java { srcDirs = [ file('src/plugin/java') ] }
        resources { srcDirs = [ file('src/plugin/resources') ] }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    lib 'org.ow2.asm:asm:9.7'
    lib 'org.ow2.asm:asm-tree:9.7'
    libImpl 'org.jetbrains:annotations:24.1.0'
    libImpl 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation sourceSets.lib.output
}

application.mainClass = 'org.moddingx.ljc.Main'

gradlePlugin {
    plugins {
        plugin {
            id = 'org.moddingx.ljc'
            implementationClass = 'org.moddingx.ljc.gradle.LjcPlugin'
        }
    }
}

jar {
    from sourceSets.lib.output
    manifest {
        attributes 'Main-Class': application.mainClass.get()
    }
}

task fatjar(type: Jar) {
    archiveClassifier = 'fatjar'
    manifest = jar.manifest
    with jar
}
build.dependsOn fatjar

task configureFatJar {
    doLast {
        configurations.libResolvable.each { dep ->
            fatjar.from(project.zipTree(dep)) {
                exclude 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'module-info.class'
                duplicatesStrategy DuplicatesStrategy.INCLUDE
            }
        }
    }
    outputs.upToDateWhen { false }
}
fatjar.dependsOn configureFatJar

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.lib.allSource
    from sourceSets.main.allSource
}
build.dependsOn sourcesJar

publishing {
    publications {
        project.afterEvaluate {
            pluginMaven {
                artifact sourcesJar
                pom {
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name 'moddingx'
            url 'https://maven.moddingx.org/release'
            credentials(PasswordCredentials)
        }
    }
}
